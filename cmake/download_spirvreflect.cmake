# download and install SPIRV-Reflect

if (${VTC_ENABLE_SPIRVREFLECT})
	set( VTC_EXTERNAL_SPIRVREFLECT_PATH "" CACHE PATH "path to SPIRV-Reflect source" )
	
	# reset to default
	if (NOT EXISTS "${VTC_EXTERNAL_SPIRVREFLECT_PATH}/spirv_reflect.h")
		message( STATUS "SPIRV-Reflect is not found in \"${VTC_EXTERNAL_SPIRVREFLECT_PATH}\"" )
		set( VTC_EXTERNAL_SPIRVREFLECT_PATH "${VTC_EXTERNALS_PATH}/SPIRV-Reflect" CACHE PATH "" FORCE )
		set( VTC_SPIRVREFLECT_REPOSITORY "https://github.com/chaoticbob/SPIRV-Reflect.git" )
	else ()
		set( VTC_SPIRVREFLECT_REPOSITORY "" )
	endif ()

	ExternalProject_Add( "Extern.SPIRV-Reflect"
        LIST_SEPARATOR		"${VTC_LIST_SEPARATOR}"
		# download
		GIT_REPOSITORY		${VTC_SPIRVREFLECT_REPOSITORY}
		GIT_TAG				master
		EXCLUDE_FROM_ALL	1
		LOG_DOWNLOAD		1
		# update
		PATCH_COMMAND		""
		UPDATE_DISCONNECTED	1
		# configure
        SOURCE_DIR			"${VTC_EXTERNAL_SPIRVREFLECT_PATH}"
		CMAKE_GENERATOR		"${CMAKE_GENERATOR}"
		CMAKE_GENERATOR_TOOLSET	"${CMAKE_GENERATOR_TOOLSET}"
        CMAKE_ARGS			"-DCMAKE_CONFIGURATION_TYPES=${VTC_EXTERNAL_CONFIGURATION_TYPES}"
							"-DCMAKE_SYSTEM_VERSION=${CMAKE_SYSTEM_VERSION}"
							"-DCMAKE_DEBUG_POSTFIX="
							"-DCMAKE_RELEASE_POSTFIX="
                            ${VTC_BUILD_TARGET_FLAGS}
		LOG_CONFIGURE 		1
		# build
		BINARY_DIR			"${CMAKE_BINARY_DIR}/build2-SPIRV-Reflect"
		BUILD_COMMAND		""
		LOG_BUILD 			1
		# install
		INSTALL_DIR 		""
		INSTALL_COMMAND		""
		LOG_INSTALL 		1
		# test
		TEST_COMMAND		""
	)
	
	set_property( TARGET "Extern.SPIRV-Reflect" PROPERTY FOLDER "External" )

    set( VTC_CONVERTER_INCLUDE_DIRS "${VTC_CONVERTER_INCLUDE_DIRS}" "${VTC_EXTERNAL_SPIRVREFLECT_PATH}" )
    set( VTC_CONVERTER_DEPENDENCIES "${VTC_CONVERTER_DEPENDENCIES}" "Extern.SPIRV-Reflect" )
	set( VTC_CONVERTER_DEFINES "${VTC_CONVERTER_DEFINES}" "VTC_ENABLE_SPIRVREFLECT" )
endif ()
